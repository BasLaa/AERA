;motor babbling episode generation.
;cup is grabbed (attached) and this is notified out-of-sync.
;hand moves to several new positions every 100ms.
;cup moves to the same positions at the same frequency with a
;slight random delay (in [0,10ms[).
;noise is injected randomly (using a pseudo random number generator).
;N.B.: inputs are set with an infinite resilience to retain them
;in memory for processing by the Correlator.
;we use value holders like vec3_holder so that the Correlator can work
;on views'oid instead of having to examine the content of objects
;as would be the case using (mk.val self_hand position an_embedded_vec3 1)

_start:(pgm
|[]
|[]
[]
   (inj []
      init_pos:(vec3_holder (vec3 0 0 0) 1)
      [SYNC_FRONT now 1 forever stdin nil]
   )
   (inj []
      (mk.val self_hand position init_pos 1)
      [SYNC_FRONT now 1 forever stdin nil]
   )
   (inj []
      (noise (rnd 1000) 1)
      [SYNC_FRONT (+ now (rnd 10000)) 1 forever stdin nil]
   )
   (inj []
      (mk.val self_hand attachment cup 1)
      [SYNC_FRONT (+ now (rnd 10000)) 1 forever stdin nil]
   )
1
)
|[]

i_start:(ipgm _start |[] 90000us 0 1)
[]
   [SYNC_FRONT now 0 1 root nil 1]

pos_updater:(pgm
|[]
[]
   []
      (ptn (mk.val self_hand position pos#vec3_holder: ::) |[])
   |[]
   |[]
[]
   (inj []
      new_pos:(vec3_holder (+ pos.value (vec3 0 0 0.1)) 1)
      [SYNC_FRONT now 1 forever stdin nil] 
   )
   (inj []
      (mk.val self_hand position new_pos 1)
      [SYNC_FRONT t:(+ now 100000us) 1 forever stdin nil]
   )
   (inj []
      (mk.val cup position new_pos 1)
      [SYNC_FRONT (+ t (rnd 10000)) 1 forever stdin nil]
   )
1
)
|[]

i_pos_updater:(ipgm pos_updater |[] 0us 0 1)
[]
   [SYNC_FRONT now 0 forever stdin nil 1]

noise_updater:(pgm
|[]
[]
   []
      (ptn (noise ::) |[])
   |[]
   |[]
[]
   (inj []
      (noise (rnd 1000) 1)
      [SYNC_FRONT (+ now (rnd 40000)) 1 forever stdin nil]
   )
1
)
|[]

i_noise_updater:(ipgm noise_updater |[] 0us 0 1)
[]
   [SYNC_FRONT now 0 forever stdin nil 1]

grab_updater:(pgm
|[]
[]
   []
      (ptn m:(mk.val : attachment ::) |[])
   |[]
   |[]
[]
   (inj []
      m
      [SYNC_FRONT (+ now (rnd 50000)) 1 forever stdin nil]
   )
1
)
|[]

i_grab_updater:(ipgm grab_updater |[] 0us 0 1)
[]
   [SYNC_FRONT now 0 forever stdin nil 1]

episode_notifier:(pgm
|[]
|[]
[]
   (inj []
      (episode_end now 1)
      [SYNC_FRONT now 1 forever stdin nil]
   )
1
)
|[]

i_episode_notifier.(ipgm episode_notifier |[] 1000000us 0 1)
[]
   [SYNC_FRONT now 0 forever stdin nil 1]


i_correlator:(icpp_pgm "correlator" |[] 0us 1 1)
[]
   [SYNC_FRONT now 0 forever stdin nil 1]
